buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

plugins {
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.40"
}

subprojects {
    group 'com.arboratum'
    version '0.1.19'

    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    repositories {
        jcenter()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    }
}

configure(subprojects.findAll {it.name != 'fastbeangen-example'}) {

    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'


    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    def pomConfig = {
        licenses {
            license {
                name "GNU General Public License, Version 3"
                url "http://www.gnu.org/licenses/gpl-3.0.html"
                distribution "manual"
            }
        }
        developers {
            developer {
                id "gpicron"
                name "Geoffrey Picron"
                email "geoffrey.picron@arboratum.com"
            }
        }
        scm {
            url 'https://github.com/Arboratum-Open/fastbeangen.git'
        }
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId project.group
                artifactId project.name
                version project.version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'FastBeanGen is an Idempotent Random bean generator')
                    root.appendNode('name', 'FastBeanGen - ' + project.name)
                    root.appendNode('url', 'https://github.com/Arboratum-Open/fastbeangen.git')
                    root.children().last() + pomConfig
                }
            }
        }
    }



    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['MyPublication']
        pkg {
            repo = 'public'
            name = 'fastbeangen'

            userOrg = 'arboratum-open'
            licenses = ['GPL-3.0']
            vcsUrl = 'https://github.com/Arboratum-Open/fastbeangen.git'
            //labels = ['gear', 'gore', 'gorilla']
            publicDownloadNumbers = true
            //attributes= ['a': ['ay1', 'ay2'], 'b': ['bee'], c: 'cee'] //Optional package-level attributes
            version {
                name = project.version
                desc = 'FastBeanGen library version ' +  project.version
                released  = new Date()
                vcsTag = 'v' + project.version
            }

        }
    }

}


task generateChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    filePath = "CHANGELOG_tmp.md";
}
