
plugins {
    id "com.jfrog.bintray" version "1.7"
}


//apply plugin: "maven"
apply plugin: 'maven-publish'

dependencies {
    compile "com.esotericsoftware:reflectasm:1.11.3"

    compile group: 'dk.brics.automaton', name: 'automaton', version: '1.11-8'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    compile 'joda-time:joda-time:2.9.4'
    compile 'io.projectreactor:reactor-core:3.0.4.RELEASE'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile 'io.projectreactor.addons:reactor-test:3.0.4.RELEASE'

    testCompile group: 'com.linkedin.paldb', name: 'paldb', version: '1.2.0'
    testCompile group: 'org.xerial.snappy', name: 'snappy-java', version: '1.1.2.6'

}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "GNU General Public License, Version 3"
            url "http://www.gnu.org/licenses/gpl-3.0.html"
            distribution "manual"
        }
    }
    developers {
        developer {
            id "gpicron"
            name "Geoffrey Picron"
            email "geoffrey.picron@arboratum.com"
        }
    }
    scm {
        url 'https://github.com/Arboratum-Open/fastbeangen.git'
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.arboratum'
            artifactId 'fastbeangen'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'FastBeanGen is an Idempotent Random bean generator')
                root.appendNode('name', 'FastBeanGen')
                root.appendNode('url', 'https://github.com/Arboratum-Open/fastbeangen.git')
                root.children().last() + pomConfig
            }
        }
    }
}



bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'public'
        name = 'fastbeangen'

        userOrg = 'arboratum-open'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/Arboratum-Open/fastbeangen.git'
        //labels = ['gear', 'gore', 'gorilla']
        publicDownloadNumbers = true
        //attributes= ['a': ['ay1', 'ay2'], 'b': ['bee'], c: 'cee'] //Optional package-level attributes
        version {
            name = project.version
            desc = 'FastBeanGen library version ' +  project.version
            released  = new Date()
            vcsTag = 'v' + project.version
        }

    }
}